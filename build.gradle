plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'java'
  id "io.freefair.lombok" version "6.6.3"
  id 'io.spring.dependency-management' version '1.1.4'
  id 'org.springframework.boot' version '3.4.1'
  id 'com.github.kt3k.coveralls' version '2.12.2'
  id 'com.github.ben-manes.versions' version '0.51.0'
  id 'org.sonarqube' version '4.3.0.3225'
  id 'uk.gov.hmcts.java' version '0.12.63'
  id 'org.owasp.dependencycheck' version '10.0.4'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << "-Xlint:unchecked"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = false
}

tasks.register('functional', Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.register('integration', Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

tasks.register('fortifyScan', JavaExec) {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
  ignoreExitValue = true
}

tasks.register('smoke', Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '9.3'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks.named('sonarqube') {
  dependsOn jacocoTestReport
}

project.tasks.named('check') {
  dependsOn integration
}

sonarqube {
  properties {
    property "sonar.organization", "hmcts"
    property "sonar.projectName", "sscs-ccd-case-migration"
    property "sonar.projectKey", "sscs-ccd-case-migration"
    property "sonar.exclusions", "**/exception/*.java,**/hmc/*.java,**/domain/*.java,**/common/*.java,**/migration/auth/AuthTokenGeneratorConfiguration.java,**/migration/CaseMigrationRunner.java,**/ccd/HttpMessageConverterConfiguration.java"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

apply from: './gradle/suppress.gradle'
// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

ext {
  lombokVersion = "1.18.30"
  junitJupiterVersion = '5.11.4'
  junitVintageVersion = '5.11.4'
  powermockVersion = '2.0.9'
  springCloudVersion = '2024.0.0'
}

ext['jackson.version'] = '2.17.2'
ext['snakeyaml.version'] = '2.0'

dependencies {
  implementation group: 'org.json', name: 'json', version: '20240303'
  implementation("org.springframework.cloud:spring-cloud-starter-bootstrap")
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBoot.class.package.implementationVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBoot.class.package.implementationVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: springBoot.class.package.implementationVersion
  implementation group: 'org.springframework', name: 'spring-context-support'
  implementation group: 'org.springframework.retry', name: 'spring-retry'
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.3'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.0'
  implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.0.3'
  implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '13.3'
  implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: '2.17.2'

  implementation group: 'com.github.hmcts', name: 'sscs-common', version: '2819a70'

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBoot.class.package.implementationVersion
  testImplementation 'org.wiremock.integrations:wiremock-spring-boot:3.6.0'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.7.0'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.4'
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.0'
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.3.1'
  testImplementation group: 'io.rest-assured', name: 'xml-path', version: '5.3.1'
  testImplementation group: 'io.rest-assured', name: 'json-path', version: '5.3.1'

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBoot.class.package.implementationVersion

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junitJupiterVersion
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
  testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: junitVintageVersion
  testImplementation group:'org.mockito', name: 'mockito-junit-jupiter', version: '5.11.0'
  testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: powermockVersion
  testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: powermockVersion
  testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.11.4'
  implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.1', classifier: 'all'

  implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.5'
  implementation group: 'commons-io', name: 'commons-io', version: '2.15.1'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-core'
  implementation group: 'org.springframework.security', name: 'spring-security-config'
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }

  dependencies {
    dependency 'com.google.guava:guava:32.1.1-jre'

    dependency group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'

    dependencySet(group: 'org.codehaus.groovy', version: '3.0.20') {
      entry 'groovy'
      entry 'groovy-xml'
      entry 'groovy-json'
    }

  }
}

application {
  mainClass.set('uk.gov.hmcts.reform.migration.CaseMigrationRunner')
}

bootJar {
  archiveFileName = 'sscs-ccd-case-migration.jar'
  archiveBaseName = 'sscs-ccd-case-migration'
  archiveVersion = project.version.toString()
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

configurations.configureEach {
  exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
  exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
  exclude group: 'org.springframework.security', module: 'spring-security-rsa'
}

test {
  timeout = Duration.ofMinutes(30)
  environment("AZURE_APPLICATIONINSIGHTS_INSTRUMENTATIONKEY", "some-key")
  systemProperty 'java.locale.providers', 'COMPAT'

  useJUnitPlatform()

  testLogging {
    events "failed"
    exceptionFormat "short"

    debug {
      events "passed", "started", "skipped", "failed"
      exceptionFormat "full"
    }

    info.events = ["failed", "skipped"]
  }

  reports {
    html.required = true
  }

  jvmArgs = [
    '--add-modules', 'java.se',
    '--add-exports', 'java.base/jdk.internal.ref=ALL-UNNAMED',
    '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
    '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
    '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
    '--add-opens', 'java.management/sun.management=ALL-UNNAMED',
    '--add-opens', 'jdk.management/com.sun.management.internal=ALL-UNNAMED'
  ]
}
